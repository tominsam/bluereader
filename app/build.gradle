apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    defaultConfig {
        applicationId "org.movieos.bluereader"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 8
        versionName "0.2"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    dataBinding {
        enabled = true
    }

    signingConfigs {
        release
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

dependencies {
    implementation "com.android.support:appcompat-v7:$support_version"
    implementation "com.android.support:design:$support_version"
    implementation "com.android.support:customtabs:$support_version"
    implementation "com.google.android.gms:play-services-auth:$play_services_version"

    // Kotlin dependencies
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Architecture components
    implementation "android.arch.lifecycle:runtime:1.1.1"
    implementation "android.arch.lifecycle:livedata:1.1.1"
    implementation "android.arch.persistence.room:runtime:1.1.1"
    kapt "android.arch.persistence.room:compiler:1.1.1"

    // http://square.github.io/retrofit/
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'

    // https://github.com/google/gson
    implementation 'com.google.code.gson:gson:2.8.2'

    // https://github.com/JakeWharton/timber
    implementation 'com.jakewharton.timber:timber:4.5.1'

    // https://github.com/greenrobot/EventBus
    implementation 'org.greenrobot:eventbus:3.0.0'
}

kapt {
    // Seems to be needed for view bindings
    generateStubs = true
}

// load signing information based on gradle.properties
// http://www.thoughtworks.com/insights/blog/signing-open-source-android-apps-without-disclosing-passwords
def props = new Properties()
props.load(new FileInputStream(rootProject.file("release.properties")))
android.signingConfigs.release.keyAlias = props.keyAlias
android.signingConfigs.release.storeFile = file(props.keyStore)
android.signingConfigs.release.storePassword = props.keyStorePassword
android.signingConfigs.release.keyPassword = props.keyAliasPassword
