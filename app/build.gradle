apply plugin: 'com.android.application'
apply plugin: 'com.github.triplet.play'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.0"

    playAccountConfigs {
        defaultAccountConfig {
            jsonFile = file('../play_store_keys.json')
        }
    }

    defaultConfig {
        applicationId "org.movieos.bluereader"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode 8
        versionName "0.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        playAccountConfig = playAccountConfigs.defaultAccountConfig
    }

    dataBinding {
        enabled = true
    }

    signingConfigs {
        release
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    play {
        track = 'beta'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

dependencies {
    compile "com.android.support:appcompat-v7:$support_version"
    compile "com.android.support:design:$support_version"
    compile "com.android.support:customtabs:$support_version"

    compile "com.google.android.gms:play-services-auth:$play_services_version"
    compile "com.google.firebase:firebase-crash:$play_services_version"

    // Kotlin dependencies
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    kapt "com.android.databinding:compiler:$android_plugin_version"

    // Architecture components
    compile "android.arch.lifecycle:runtime:$arch_version"
    compile "android.arch.lifecycle:extensions:$arch_version"
    compile "android.arch.persistence.room:runtime:$arch_version"
    kapt "android.arch.persistence.room:compiler:$arch_version"

    // http://square.github.io/retrofit/
    compile 'com.squareup.retrofit2:retrofit:2.2.0'
    compile 'com.squareup.retrofit2:converter-gson:2.2.0'
    compile 'com.squareup.okhttp3:okhttp:3.8.1'

    // https://github.com/google/gson
    compile 'com.google.code.gson:gson:2.8.0'

    // https://github.com/JakeWharton/timber
    compile 'com.jakewharton.timber:timber:4.5.1'

    // https://github.com/greenrobot/EventBus
    compile 'org.greenrobot:eventbus:3.0.0'


    compile 'com.facebook.stetho:stetho:1.5.0'
    compile 'com.facebook.stetho:stetho-okhttp3:1.5.0'

}

kapt {
    // Seems to be needed for view bindings
    generateStubs = true
}

// load signing information based on gradle.properties
// http://www.thoughtworks.com/insights/blog/signing-open-source-android-apps-without-disclosing-passwords
def props = new Properties()
props.load(new FileInputStream(rootProject.file("release.properties")))
android.signingConfigs.release.keyAlias = props.keyAlias
android.signingConfigs.release.storeFile = file(props.keyStore)
android.signingConfigs.release.storePassword = props.keyStorePassword
android.signingConfigs.release.keyPassword = props.keyAliasPassword


apply plugin: 'com.google.gms.google-services'
