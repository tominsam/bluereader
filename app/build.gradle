apply plugin: 'com.android.application'
apply plugin: 'com.github.triplet.play'
apply plugin: 'kotlin-android'

buildscript {
    ext.support_version = '26.0.0-beta2'
}

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.0"

    playAccountConfigs {
        defaultAccountConfig {
            jsonFile = file('../play_store_keys.json')
        }
    }

    defaultConfig {
        applicationId "org.movieos.bluereader"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode 5
        versionName "0.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        playAccountConfig = playAccountConfigs.defaultAccountConfig
    }

    dataBinding {
        enabled = true
    }

    signingConfigs {
        release
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    play {
        track = 'beta'
    }

}

dependencies {
    compile "com.android.support:appcompat-v7:$support_version"
    compile "com.android.support:design:$support_version"
    compile "com.android.support:customtabs:$support_version"

    compile 'com.google.android.gms:play-services-auth:11.0.1'

    // http://square.github.io/retrofit/
    compile 'com.squareup.retrofit2:retrofit:2.2.0'
    compile 'com.squareup.retrofit2:converter-gson:2.2.0'
    compile 'com.squareup.okhttp3:okhttp:3.7.0'

    // https://github.com/google/gson
    compile 'com.google.code.gson:gson:2.8.0'

    // https://github.com/JakeWharton/timber
    compile 'com.jakewharton.timber:timber:4.5.1'

    // https://github.com/greenrobot/EventBus
    compile 'org.greenrobot:eventbus:3.0.0'

    // Kotlin dependencies
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    kapt "com.android.databinding:compiler:$android_plugin_version"

    compile 'com.facebook.stetho:stetho:1.5.0'
    compile 'com.facebook.stetho:stetho-okhttp3:1.5.0'

    compile "android.arch.lifecycle:runtime:1.0.0-alpha3"
    compile "android.arch.lifecycle:extensions:1.0.0-alpha3"
    compile "android.arch.persistence.room:runtime:1.0.0-alpha3"
    kapt "android.arch.persistence.room:compiler:1.0.0-alpha3"
}

kapt {
    // Seems to be needed for view bindings
    generateStubs = true
}

// load signing information based on gradle.properties
// http://www.thoughtworks.com/insights/blog/signing-open-source-android-apps-without-disclosing-passwords
def props = new Properties()
props.load(new FileInputStream(rootProject.file("release.properties")))
android.signingConfigs.release.keyAlias = props.keyAlias
android.signingConfigs.release.storeFile = file(props.keyStore)
android.signingConfigs.release.storePassword = props.keyStorePassword
android.signingConfigs.release.keyPassword = props.keyAliasPassword
